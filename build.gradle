
// For those who want the bleeding edge
buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.ajoberstar:gradle-git:1.6.0'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.ajoberstar.grgit'

// Compile for Java 8
sourceCompatibility = 1.8
targetCompatibility = 1.8

// Make Eclipse recognize project as Gradle project
eclipse.project {
    natures 'org.eclipse.buildship.core.gradleprojectnature'
    natures 'org.springsource.ide.eclipse.gradle.core.nature'
}

// Enable incremental compilation
tasks.withType(JavaCompile) {
   options.incremental = true
}

version = "1.80pr1"
group = "dan200.computercraft"
archivesBaseName = "ComputerCraft"

minecraft {
    version = "1.12-14.21.1.2387"
    runDir = "run"
    replace '${version}', project.version

    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not allways work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = "snapshot_20170629"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
}

configurations {
    shade
    compile.extendsFrom shade
}

dependencies {
    shade project(':luaj')
}

jar {
    manifest {
        attributes('FMLAT': 'computercraft_at.cfg')
    }
}

import org.ajoberstar.grgit.Grgit

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    def grgit = Grgit.open(dir: '.')
    inputs.property "commithash", grgit.head().id

    def blacklist = ['GitHub', 'dan200', 'Daniel Ratcliffe']
    Set<String> contributors = []

    grgit.log().each {
        if (!blacklist.contains(it.author.name)) contributors.add(it.author.name)
        if (!blacklist.contains(it.committer.name)) contributors.add(it.committer.name)
    }

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        include 'assets/computercraft/lua/rom/help/credits.txt'

        expand 'version':project.version,
               'mcversion':project.minecraft.version,
               'gitcontributors':contributors.sort(false, String.CASE_INSENSITIVE_ORDER).join('\n')
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
        exclude 'assets/computercraft/lua/rom/help/credits.txt'
    }
}

jar {
    configurations.shade.each {
        from(it.isDirectory() ? it : zipTree(it)) { exclude 'META-INF', 'META-INF/**' }
    }
    
    into("api", { from (sourceSets.main.java) {
        include "dan200/computercraft/api/**/*.java"
    }})
}

task countLines {
    group = 'other'
    description = 'Count lines of Java, JSON, and Lua'
    
    doLast {
        println "Java: " + sourceSets.main.allSource.files.findAll {it.name.endsWith ".java"}.sum {(it.text =~ /\n/).count}
        println "JSON: " + sourceSets.main.allSource.files.findAll {it.name.endsWith ".json"}.sum {(it.text =~ /\n/).count}
        
        def lualines = 0
        new File(sourceSets.main.resources.srcDirs[0], 'assets/computercraft/lua')
            .eachFileRecurse(groovy.io.FileType.FILES,{ lualines += (it.text =~ /\n/).count })
        println "Lua: $lualines"
    }
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint"
    }
}

runClient { outputs.upToDateWhen { false } }
runServer { outputs.upToDateWhen { false } }
